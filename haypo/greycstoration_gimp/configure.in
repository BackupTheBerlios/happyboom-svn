AC_PREREQ(2.54)

m4_define([plugin_name], [greycstoration])

m4_define([plugin_major_version], [0])
m4_define([plugin_minor_version], [1])
m4_define([plugin_micro_version], [3])
m4_define([plugin_version],
          [plugin_major_version.plugin_minor_version.plugin_micro_version])

AC_INIT([plugin_name], [plugin_version])

AC_DEFINE(PLUGIN_NAME, PACKAGE_NAME, [Plug-In name])
AC_DEFINE(PLUGIN_VERSION, PACKAGE_VERSION, [Plug-In version])
AC_DEFINE(PLUGIN_MAJOR_VERSION, plugin_major_version, [Plug-In major version])
AC_DEFINE(PLUGIN_MINOR_VERSION, plugin_minor_version, [Plug-In minor version])
AC_DEFINE(PLUGIN_MICRO_VERSION, plugin_micro_version, [Plug-In micro version])

AC_CONFIG_SRCDIR([src/main.cpp])
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(no-define)
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

dnl Add --enable-maintainer-mode option
AM_MAINTAINER_MODE

dnl Check for C++ compiler, and turn on optimizations if we have gcc.
if test "x$CXXFLAGS" = "x" ; then
	CXXFLAGS=""
fi
AC_PROG_CXX
if test "x$CXXFLAGS" = "x" && test "x$GXX" = "xyes" ; then
	CXXFLAGS="-O3 -ffast-math"
fi

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GXX" = "xyes"; then
	case " $CXXFLAGS " in
		*[\ \	]-Wall[\ \	]*) ;;
		*) CXXFLAGS="$CXXFLAGS -Wall" ;;
	esac
fi
changequote([,])dnl

dnl Check for Gimp 2.x
GIMP_REQUIRED_VERSION=2.2.0
PKG_CHECK_MODULES(GIMP,
	[gimp-2.0 >= $GIMP_REQUIRED_VERSION,
	 gimpui-2.0 >= $GIMP_REQUIRED_VERSION])
AC_SUBST(GIMP_CFLAGS)
AC_SUBST(GIMP_LIBS)
GIMP_LIBDIR=`$PKG_CONFIG --variable=gimplibdir gimp-2.0`
AC_SUBST(GIMP_LIBDIR)

dnl Check for GTK+ 2.x
GTK_REQUIRED_VERSION=2.6.0
PKG_CHECK_MODULES(GTK,
	[gtk+-2.0 >= $GTK_REQUIRED_VERSION])
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)

dnl i18n stuff
ALL_LINGUAS="fr ru"
GETTEXT_PACKAGE=gimp20-greycstoration
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,
	"$GETTEXT_PACKAGE",
	[The gettext translation domain.])
AC_PROG_INTLTOOL
AM_GLIB_GNU_GETTEXT
AC_CHECK_FUNCS(bind_textdomain_codeset)
LOCALEDIR="${datadir}/locale"
if test "$DATADIRNAME" = "lib"; then
	LOCALEDIR="$libdir/locale"
fi
AC_SUBST(LOCALEDIR)

dnl Output files
AC_CONFIG_FILES([
Makefile
src/Makefile
po/Makefile.in
])
AC_OUTPUT
